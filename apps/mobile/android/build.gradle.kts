import com.android.build.api.dsl.ApplicationExtension
import com.android.build.api.dsl.LibraryExtension

val unifiedNdkVersion = "30.0.16248370"
val unifiedCompileSdk = 36

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

val newBuildDir: Directory =
    rootProject.layout.buildDirectory
        .dir("../../build")
        .get()
rootProject.layout.buildDirectory.value(newBuildDir)

subprojects {
    val newSubprojectBuildDir: Directory = newBuildDir.dir(project.name)
    project.layout.buildDirectory.value(newSubprojectBuildDir)
}
subprojects {
    if (name != "app") {
        afterEvaluate {
            extensions.findByType(ApplicationExtension::class.java)?.apply {
                compileSdk = unifiedCompileSdk
                ndkVersion = unifiedNdkVersion
            }
            extensions.findByType(LibraryExtension::class.java)?.apply {
                compileSdk = unifiedCompileSdk
                ndkVersion = unifiedNdkVersion
            }
        }
    }
    project.evaluationDependsOn(":app")
}

tasks.register<Delete>("clean") {
    delete(rootProject.layout.buildDirectory)
}
